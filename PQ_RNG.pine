//@version=6
indicator(title = "Swoosh Range Detector [PQ_MOD]", shorttitle = "RNG0.1.9", overlay = true, max_lines_count=500, max_labels_count=500, max_boxes_count=500, max_polylines_count=100, max_bars_back=5000, dynamic_requests=true)

//------------------------------------------------------------------------------
//Settings Range
//-----------------------------------------------------------------------------{
INPUT_RANGE_LENGTH = input.int(10, 'Minimum Range Length', minval = 2)
INPUT_RANGE_WIDTH  = input.float(0.8, 'Range Width', minval = 0, step = 0.1)
INPUT_ATR_LENGTH   = input.int(14, 'ATR Length', minval = 1)


//------------------------------------------------------------------------------
//Settings Predictive
//-----------------------------------------------------------------------------{
INPUT_PRED_LENGTH = input.int(250, 'Predictive Range Length', minval = 2)
INPUT_PRED_FACTOR = input.float(3.0, 'Factor', minval = 0, step = .5)
INPUT_TIMEFRAME   = input.timeframe('', 'Timeframe')
INPUT_SOURCE      = input(hlcc4, 'Source')

//---------------------------------------------------------------------------------------------------------------------}
//CONSTANTS & STRINGS & INPUTS
//---------------------------------------------------------------------------------------------------------------------{
BULLISH_LEG                     = 1
BEARISH_LEG                     = 0

BULLISH                         = +1
BEARISH                         = -1

GREEN                           = #089981
RED                             = #F23645
BLUE                            = #2157f3
GRAY                            = #878b94
MONO_BULLISH                    = #b2b5be
MONO_BEARISH                    = #5d606b

HISTORICAL                      = 'Historical'
PRESENT                         = 'Present'

COLORED                         = 'Colored'
MONOCHROME                      = 'Monochrome'

ALL                             = 'All'
BOS                             = 'BOS'
CHOCH                           = 'CHoCH'
PREMIUM                         = 'PRM'
DISCOUNT                        = 'DIS'
EQUILIBRIUM                     = 'EQL'
STRONG_HIGH                     = 'STH'
WEAK_HIGH                       = 'WEH'
STRONG_LOW                      = 'SLO'
WEAK_LOW                        = 'WEL'

TINY                            = size.tiny
SMALL                           = size.small
NORMAL                          = size.normal

ATR                             = 'ATR'
RANGE                           = 'Cumulative Mean Range'

CLOSE                           = 'Close'
HIGHLOW                         = 'High/Low'

SOLID                           = '⎯⎯⎯'
DASHED                          = '----'
DOTTED                          = '····'

SMART_GROUP                     = 'Smart Money Concepts'
INTERNAL_GROUP                  = 'Real Time Internal Structure'
SWING_GROUP                     = 'Real Time Swing Structure'
BLOCKS_GROUP                    = 'Order Blocks'
EQUAL_GROUP                     = 'EQH/EQL'
GAPS_GROUP                      = 'Fair Value Gaps'
LEVELS_GROUP                    = 'Highs & Lows MTF'
ZONES_GROUP                     = 'Premium & Discount Zones'

TOOLTIP_MODE                     = 'Allows to display historical Structure or only the recent ones'
TOOLTIP_STYLE                    = 'Indicator color theme'
TOOLTIP_SHOW_TREND               = 'Display additional candles with a color reflecting the current trend detected by structure'
TOOLTIP_SHOW_INTERNALS           = 'Display internal market structure'
TOOLTIP_INT_FILTER_CONFLUENCE    = 'Filter non significant internal structure breakouts'
TOOLTIP_SHOW_STRUCTURE           = 'Display swing market Structure'
TOOLTIP_SHOW_SWINGS              = 'Display swing point as labels on the chart'
TOOLTIP_SHOW_HL_SWINGS           = 'Highlight most recent strong and weak high/low points on the chart'
TOOLTIP_SHOW_INT_OB              = 'Display internal order blocks on the chart\n\nNumber of internal order blocks to display on the chart'
TOOLTIP_SHOW_SWING_OB            = 'Display swing order blocks on the chart\n\nNumber of internal swing blocks to display on the chart'
TOOLTIP_OB_FILTER                = 'Method used to filter out volatile order blocks \n\nIt is recommended to use the cumulative mean range method when a low amount of data is available'
TOOLTIP_OB_MITIGATION            = 'Select what values to use for order block mitigation'
TOOLTIP_SHOW_EQ                  = 'Display equal highs and equal lows on the chart'
TOOLTIP_EQ_LENGTH                = 'Number of bars used to confirm equal highs and equal lows'
TOOLTIP_EQ_THRESHOLD             = 'Sensitivity threshold in a range (0, 1) used for the detection of equal highs & lows\n\nLower values will return fewer but more pertinent results'
TOOLTIP_SHOW_FVG                 = 'Display fair values gaps on the chart'
TOOLTIP_FVG_THRESHOLD            = 'Filter out non significant fair value gaps'
TOOLTIP_FVG_TIMEFRAME            = 'Fair value gaps timeframe'
TOOLTIP_FVG_EXTEND               = 'Determine how many bars to extend the Fair Value Gap boxes on chart'
TOOLTIP_SHOW_PD_ZONES            = 'Display premium, discount, and equilibrium zones on chart'

INPUT_MODE                       = input.string( HISTORICAL, 'Mode',                     group = SMART_GROUP,    tooltip = TOOLTIP_MODE, options = [HISTORICAL, PRESENT])
INPUT_STYLE                      = input.string( COLORED,    'Style',                    group = SMART_GROUP,    tooltip = TOOLTIP_STYLE,options = [COLORED, MONOCHROME])
INPUT_SHOW_TREND                 = input(        false,      'Color Candles',            group = SMART_GROUP,    tooltip = TOOLTIP_SHOW_TREND)

INPUT_SHOW_INTERNALS             = input(        true,       'Show Internal Structure',  group = INTERNAL_GROUP, tooltip = TOOLTIP_SHOW_INTERNALS)
INPUT_INTERNAL_BULL_STRUCTURE    = input.string( ALL,        'Bullish Structure',        group = INTERNAL_GROUP, inline = 'ibull', options = [ALL,BOS,CHOCH])
INPUT_INTERNAL_BULL_COLOR        = input(        GREEN,      '',                         group = INTERNAL_GROUP, inline = 'ibull')
INPUT_INTERNAL_BEAR_STRUCTURE    = input.string( ALL,        'Bearish Structure' ,       group = INTERNAL_GROUP, inline = 'ibear', options = [ALL,BOS,CHOCH])
INPUT_INTERNAL_BEAR_COLOR        = input(        RED,        '',                         group = INTERNAL_GROUP, inline = 'ibear')
INPUT_INT_FILTER_CONFLUENCE      = input(        true,      'Confluence Filter',        group = INTERNAL_GROUP, tooltip = TOOLTIP_INT_FILTER_CONFLUENCE)
INPUT_INTERNAL_LABEL_SIZE        = input.string( TINY,       'Internal Label Size',      group = INTERNAL_GROUP, options = [TINY,SMALL,NORMAL])

INPUT_SHOW_SWING_STRUCTURE       = input(        true,       'Show Swing Structure',     group = SWING_GROUP,    tooltip = TOOLTIP_SHOW_STRUCTURE)
INPUT_SWING_BULL_STRUCTURE       = input.string( ALL,        'Bullish Structure',        group = SWING_GROUP,    inline = 'bull',    options = [ALL,BOS,CHOCH])
INPUT_SWING_BULL_COLOR           = input(        GREEN,      '',                         group = SWING_GROUP,    inline = 'bull')
INPUT_SWING_BEAR_STRUCTURE       = input.string( ALL,        'Bearish Structure',        group = SWING_GROUP,    inline = 'bear',    options = [ALL,BOS,CHOCH])
INPUT_SWING_BEAR_COLOR           = input(        RED,        '',                         group = SWING_GROUP,    inline = 'bear')
INPUT_SWING_LABEL_SIZE           = input.string( SMALL,      'Swing Label Size',         group = SWING_GROUP,    options = [TINY,SMALL,NORMAL])
INPUT_SHOW_SWINGS                = input(        false,      'Show Swings Points',       group = SWING_GROUP,    tooltip = TOOLTIP_SHOW_SWINGS,inline = 'swings')
INPUT_SWINGS_LENGTH              = input.int(    10,         '',                         group = SWING_GROUP,    minval = 10,                inline = 'swings')
INPUT_SHOW_HL_SWINGS             = input(        true,       'Show Strong/Weak High/Low',group = SWING_GROUP,    tooltip = TOOLTIP_SHOW_HL_SWINGS)

INPUT_SHOW_INTERNAL_OB           = input(        true,       'Internal Order Blocks' ,   group = BLOCKS_GROUP,   tooltip = TOOLTIP_SHOW_INT_OB,   inline = 'iob')
INPUT_INTERNAL_OB_SIZE           = input.int(    3,          '',                         group = BLOCKS_GROUP,   minval = 1, maxval = 20,                    inline = 'iob')
INPUT_SHOW_SWING_OB              = input(        false,      'Swing Order Blocks',       group = BLOCKS_GROUP,   tooltip = TOOLTIP_SHOW_SWING_OB,      inline = 'ob')
INPUT_SWING_OB_SIZE              = input.int(    3,          '',                         group = BLOCKS_GROUP,   minval = 1, maxval = 20,                    inline = 'ob') 
INPUT_OB_FILTER                  = input.string( ATR,      'Order Block Filter',       group = BLOCKS_GROUP,   tooltip = TOOLTIP_OB_FILTER,          options = [ATR, RANGE])
INPUT_OB_MITIGATION_MIN          = input.source(close, 'Order Blokc Migration Min', tooltip = TOOLTIP_OB_MITIGATION)
INPUT_OB_MITIGATION_MAX          = input.source(close, 'Order Blokc Migration Max', tooltip = TOOLTIP_OB_MITIGATION)
INPUT_INTERNAL_BULL_OB_COLOR     = input.color(color.new(BLUE, 75), 'Internal Bullish OB',    group = BLOCKS_GROUP)
INPUT_INTERNAL_BEAR_OB_COLOR     = input.color(color.new(RED, 75), 'Internal Bearish OB',    group = BLOCKS_GROUP)
INPUT_SWING_BULL_OB_COLOR        = input.color(color.new(BLUE, 90), 'Bullish OB',             group = BLOCKS_GROUP)
INPUT_SWING_BEAR_OB_COLOR        = input.color(color.new(RED, 90), 'Bearish OB',             group = BLOCKS_GROUP)

INPUT_SHOW_EQ_HL                 = input(        true,       'Equal High/Low',           group = EQUAL_GROUP,    tooltip = TOOLTIP_SHOW_EQ)
INPUT_EQ_HL_LENGTH               = input.int(    3,          'Bars Confirmation',        group = EQUAL_GROUP,    tooltip = TOOLTIP_EQ_LENGTH,      minval = 1)
INPUT_EQ_HL_THRESHOLD            = input.float(  0.1,        'Threshold',                group = EQUAL_GROUP,    tooltip = TOOLTIP_EQ_THRESHOLD,   minval = 0, maxval = 0.5, step = 0.1)
INPUT_EQ_HL_SIZE                 = input.string( TINY,       'Label Size',               group = EQUAL_GROUP,    options = [TINY,SMALL,NORMAL])

INPUT_SHOW_FVG                   = input(        true,      'Fair Value Gaps',          group = GAPS_GROUP,     tooltip = TOOLTIP_SHOW_FVG)
INPUT_FVG_AUTO_THRESHOLD         = input(        true,       'Auto Threshold',           group = GAPS_GROUP,     tooltip = TOOLTIP_FVG_THRESHOLD)
INPUT_FVG_TIMEFRAME              = input.timeframe('',       'Timeframe',                group = GAPS_GROUP,     tooltip = TOOLTIP_FVG_TIMEFRAME)
INPUT_FVG_BULL_COLOR             = input.color(color.new(GREEN, 70), 'Bullish FVG' , group = GAPS_GROUP)
INPUT_FVG_BEAR_COLOR             = input.color(color.new(RED, 70), 'Bearish FVG' , group = GAPS_GROUP)
INPUT_FVG_EXTEND                 = input.int(    5,          'Extend FVG',               group = GAPS_GROUP,     tooltip = TOOLTIP_FVG_EXTEND,       minval = 0)

INPUT_SHOW_DAILY_LEVELS          = input(        false,      'Daily',    group = LEVELS_GROUP,   inline = 'daily')
INPUT_DAILY_LEVELS_STYLE         = input.string( SOLID,      '',         group = LEVELS_GROUP,   inline = 'daily',   options = [SOLID,DASHED,DOTTED])
INPUT_DAILY_LEVELS_COLOR         = input(        color.new(BLUE, 75),       '',         group = LEVELS_GROUP,   inline = 'daily')
INPUT_SHOW_WEEKLY_LEVELS         = input(        false,      'Weekly',   group = LEVELS_GROUP,   inline = 'weekly')
INPUT_WEEKLY_LEVELS_STYLE        = input.string( SOLID,      '',         group = LEVELS_GROUP,   inline = 'weekly',  options = [SOLID,DASHED,DOTTED])
INPUT_WEEKLY_LEVELS_COLOR        = input(        color.new(BLUE, 75),       '',         group = LEVELS_GROUP,   inline = 'weekly')
INPUT_SHOW_MONTHLY_LEVELS        = input(        false,      'Monthly',   group = LEVELS_GROUP,   inline = 'monthly')
INPUT_MONTHLY_LEVELS_STYLE       = input.string( SOLID,      '',         group = LEVELS_GROUP,   inline = 'monthly', options = [SOLID,DASHED,DOTTED])
INPUT_MONTHLY_LEVELS_COLOR       = input(        color.new(BLUE, 75),       '',         group = LEVELS_GROUP,   inline = 'monthly')

INPUT_SHOW_PD_ZONES              = input(        false,      'Premium/Discount Zones',   group = ZONES_GROUP , tooltip = TOOLTIP_SHOW_PD_ZONES)
INPUT_PREMIUM_ZONE_COLOR         = input.color(  color.new(RED, 75),        'Premium Zone',             group = ZONES_GROUP)
INPUT_EQL_ZONE_COLOR             = input.color(  color.new(GRAY, 75),       'Equilibrium Zone',         group = ZONES_GROUP)
INPUT_DISCOUNT_ZONE_COLOR        = input.color(  color.new(GREEN, 75),      'Discount Zone',            group = ZONES_GROUP)

INPUT_UP_CSS       = input.color(color.new(GREEN, 75), 'Broken Upward', group = 'Style')
INPUT_DN_CSS       = input.color(color.new(RED, 75), 'Broken Downward', group = 'Style')
INPUT_UNBROKEN_CSS = input.color(color.new(BLUE, 75), 'Unbroken', group = 'Style')

//---------------------------------------------------------------------------------------------------------------------}
//DATA STRUCTURES & VARIABLES
//---------------------------------------------------------------------------------------------------------------------{
// @type                            UDT representing alerts as bool fields
// @field internalBullishBOS        internal structure custom alert
// @field internalBearishBOS        internal structure custom alert
// @field internalBullishCHoCH      internal structure custom alert
// @field internalBearishCHoCH      internal structure custom alert
// @field swingBullishBOS           swing structure custom alert
// @field swingBearishBOS           swing structure custom alert
// @field swingBullishCHoCH         swing structure custom alert
// @field swingBearishCHoCH         swing structure custom alert
// @field internalBullishOrderBlock internal order block custom alert
// @field internalBearishOrderBlock internal order block custom alert
// @field swingBullishOrderBlock    swing order block custom alert
// @field swingBearishOrderBlock    swing order block custom alert
// @field equalHighs                equal high low custom alert
// @field equalLows                 equal high low custom alert
// @field bullishFairValueGap       fair value gap custom alert
// @field bearishFairValueGap       fair value gap custom alert
type alerts
    bool internalBullishBOS         = false
    bool internalBearishBOS         = false
    bool internalBullishCHoCH       = false
    bool internalBearishCHoCH       = false
    bool swingBullishBOS            = false
    bool swingBearishBOS            = false
    bool swingBullishCHoCH          = false
    bool swingBearishCHoCH          = false
    bool internalBullishOrderBlock  = false
    bool internalBearishOrderBlock  = false
    bool swingBullishOrderBlock     = false
    bool swingBearishOrderBlock     = false
    bool equalHighs                 = false
    bool equalLows                  = false
    bool bullishFairValueGap        = false
    bool bearishFairValueGap        = false

// @type                            UDT representing last swing extremes (top & bottom)
// @field top                       last top swing price
// @field bottom                    last bottom swing price
// @field barTime                   last swing bar time
// @field barIndex                  last swing bar index
// @field lastTopTime               last top swing time
// @field lastBottomTime            last bottom swing time
type trailingExtremes
    float top
    float bottom
    int barTime
    int barIndex
    int lastTopTime
    int lastBottomTime

// @type                            UDT representing Fair Value Gaps
// @field top                       top price
// @field bottom                    bottom price
// @field bias                      bias (BULLISH or BEARISH)
// @field topBox                    top box
// @field bottomBox                 bottom box
type fairValueGap
    float top
    float bottom
    int bias
    box topBox
    box bottomBox

// @type                            UDT representing trend bias
// @field bias                      BULLISH or BEARISH
type trend
    int bias    

// @type                            UDT representing Equal Highs Lows display
// @field l_ine                     displayed line
// @field l_abel                    displayed label
type equalDisplay
    line l_ine      = na
    label l_abel    = na

// @type                            UDT representing a pivot point (swing point) 
// @field currentLevel              current price level
// @field lastLevel                 last price level
// @field crossed                   true if price level is crossed
// @field barTime                   bar time
// @field barIndex                  bar index    
type pivot
    float currentLevel
    float lastLevel
    bool crossed
    int barTime     = time
    int barIndex    = bar_index

// @type                            UDT representing an order block
// @field barHigh                   bar high
// @field barLow                    bar low
// @field barTime                   bar time
// @field bias                      BULLISH or BEARISH
type orderBlock
    float barHigh
    float barLow
    int barTime    
    int bias

// @variable                        current swing pivot high    
var pivot swingHigh                 = pivot.new(na,na,false)
// @variable                        current swing pivot low
var pivot swingLow                  = pivot.new(na,na,false)
// @variable                        current internal pivot high
var pivot internalHigh              = pivot.new(na,na,false)
// @variable                        current internal pivot low
var pivot internalLow               = pivot.new(na,na,false)
// @variable                        current equal high pivot
var pivot equalHigh                 = pivot.new(na,na,false)
// @variable                        current equal low pivot
var pivot equalLow                  = pivot.new(na,na,false)
// @variable                        swing trend bias
var trend swingTrend                = trend.new(0)
// @variable                        internal trend bias
var trend internalTrend             = trend.new(0)
// @variable                        equal high display
var equalDisplay equalHighDisplay   = equalDisplay.new()
// @variable                        equal low display
var equalDisplay equalLowDisplay    = equalDisplay.new()
// @variable                        storage for fairValueGap UDTs
var array<fairValueGap> fairValueGaps = array.new<fairValueGap>()
// @variable                        storage for parsed highs
var array<float> parsedHighs        = array.new<float>()
// @variable                        storage for parsed lows
var array<float> parsedLows         = array.new<float>()
// @variable                        storage for raw highs
var array<float> highs              = array.new<float>()
// @variable                        storage for raw lows
var array<float> lows               = array.new<float>()
// @variable                        storage for bar time values
var array<int> times                = array.new<int>()
// @variable                        last trailing swing high and low
var trailingExtremes trailing       = trailingExtremes.new()
// @variable                                storage for orderBlock UDTs (swing order blocks)
var array<orderBlock> swingOrderBlocks      = array.new<orderBlock>()
// @variable                                storage for orderBlock UDTs (internal order blocks)
var array<orderBlock> internalOrderBlocks   = array.new<orderBlock>()
// @variable                                storage for swing order blocks boxes
var array<box> swingOrderBlocksBoxes        = array.new<box>()
// @variable                                storage for internal order blocks boxes
var array<box> internalOrderBlocksBoxes     = array.new<box>()
// @variable                        color for swing bullish structures
var swingBullishColor               = INPUT_STYLE == MONOCHROME ? MONO_BULLISH : INPUT_SWING_BULL_COLOR
// @variable                        color for swing bearish structures
var swingBearishColor               = INPUT_STYLE == MONOCHROME ? MONO_BEARISH : INPUT_SWING_BEAR_COLOR
// @variable                        color for bullish fair value gaps
var fairValueGapBullishColor        = INPUT_STYLE == MONOCHROME ? color.new(MONO_BULLISH,70) : INPUT_FVG_BULL_COLOR
// @variable                        color for bearish fair value gaps
var fairValueGapBearishColor        = INPUT_STYLE == MONOCHROME ? color.new(MONO_BEARISH,70) : INPUT_FVG_BEAR_COLOR
// @variable                        color for premium zone
var premiumZoneColor                = INPUT_STYLE == MONOCHROME ? MONO_BEARISH : INPUT_PREMIUM_ZONE_COLOR
// @variable                        color for discount zone
var discountZoneColor               = INPUT_STYLE == MONOCHROME ? MONO_BULLISH : INPUT_DISCOUNT_ZONE_COLOR 
// @variable                        bar index on current script iteration
varip int currentBarIndex           = bar_index
// @variable                        bar index on last script iteration
varip int lastBarIndex              = bar_index
// @variable                        alerts in current bar
alerts currentAlerts                = alerts.new()
// @variable                        time at start of chart
var initialTime                     = time

// we create the needed boxes for displaying order blocks at the first execution
if barstate.isfirst
    if INPUT_SHOW_SWING_OB
        for index = 1 to INPUT_SWING_OB_SIZE
            swingOrderBlocksBoxes.push(box.new(na,na,na,na,xloc = xloc.bar_time,extend = extend.right))
    if INPUT_SHOW_INTERNAL_OB
        for index = 1 to INPUT_INTERNAL_OB_SIZE
            internalOrderBlocksBoxes.push(box.new(na,na,na,na,xloc = xloc.bar_time,extend = extend.right))

// @variable                        source to use in bearish order blocks mitigation
// bearishOrderBlockMitigationSource   = orderBlockMitigationInput == CLOSE ? close : high
bearishOrderBlockMitigationSource   = INPUT_OB_MITIGATION_MAX
// @variable                        source to use in bullish order blocks mitigation
// bullishOrderBlockMitigationSource   = orderBlockMitigationInput == CLOSE ? close : low
bullishOrderBlockMitigationSource   = INPUT_OB_MITIGATION_MIN
// @variable                        default volatility measure
atrMeasure                          = ta.atr(INPUT_PRED_LENGTH)
// @variable                        parsed volatility measure by user settings
volatilityMeasure                   = INPUT_OB_FILTER == ATR ? atrMeasure : ta.cum(ta.tr)/bar_index
// @variable                        true if current bar is a high volatility bar
highVolatilityBar                   = (high - low) >= (2 * volatilityMeasure)
// @variable                        parsed high
parsedHigh                          = highVolatilityBar ? low : high
// @variable                        parsed low
parsedLow                           = highVolatilityBar ? high : low

// we store current values into the arrays at each bar
parsedHighs.push(parsedHigh)
parsedLows.push(parsedLow)
highs.push(high)
lows.push(low)
times.push(time)


//---------------------------------------------------------------------------------------------------------------------}
//USER-DEFINED FUNCTIONS
//---------------------------------------------------------------------------------------------------------------------{
// @function            Get the value of the current leg, it can be 0 (bearish) or 1 (bullish)
// @returns             int
leg(int size) =>
    var leg     = 0    
    newLegHigh  = high[size] > ta.highest( size)
    newLegLow   = low[size]  < ta.lowest(  size)
    
    if newLegHigh
        leg := BEARISH_LEG
    else if newLegLow
        leg := BULLISH_LEG
    leg

// @function            Identify whether the current value is the start of a new leg (swing)
// @param leg           (int) Current leg value
// @returns             bool
startOfNewLeg(int leg)      => ta.change(leg) != 0

// @function            Identify whether the current level is the start of a new bearish leg (swing)
// @param leg           (int) Current leg value
// @returns             bool
startOfBearishLeg(int leg)  => ta.change(leg) == -1

// @function            Identify whether the current level is the start of a new bullish leg (swing)
// @param leg           (int) Current leg value
// @returns             bool
startOfBullishLeg(int leg)  => ta.change(leg) == +1

// @function            create a new label
// @param labelTime     bar time coordinate
// @param labelPrice    price coordinate
// @param tag           text to display
// @param labelColor    text color
// @param labelStyle    label style
// @returns             label ID
drawLabel(int labelTime, float labelPrice, string tag, color labelColor, string labelStyle) =>    
    var label l_abel = na

    if INPUT_MODE == PRESENT
        l_abel.delete()

    l_abel := label.new(chart.point.new(labelTime,na,labelPrice),tag,xloc.bar_time,color=color(na),textcolor=labelColor,style = labelStyle,size = size.small)

// @function            create a new line and label representing an EQH or EQL
// @param p_ivot        starting pivot
// @param level         price level of current pivot
// @param size          how many bars ago was the current pivot detected
// @param equalHigh     true for EQH, false for EQL
// @returns             label ID
drawEqualHighLow(pivot p_ivot, float level, int size, bool equalHigh) =>
    equalDisplay e_qualDisplay = equalHigh ? equalHighDisplay : equalLowDisplay
    
    string tag          = 'EQL'
    color equalColor    = swingBullishColor
    string labelStyle   = label.style_label_up

    if equalHigh
        tag         := 'EQH'
        equalColor  := swingBearishColor
        labelStyle  := label.style_label_down

    if INPUT_MODE == PRESENT
        line.delete(    e_qualDisplay.l_ine)
        label.delete(   e_qualDisplay.l_abel)
        
    e_qualDisplay.l_ine     := line.new(chart.point.new(p_ivot.barTime,na,p_ivot.currentLevel), chart.point.new(time[size],na,level), xloc = xloc.bar_time, color = equalColor, style = line.style_dotted)
    labelPosition           = math.round(0.5*(p_ivot.barIndex + bar_index - size))
    e_qualDisplay.l_abel    := label.new(chart.point.new(na,labelPosition,level), tag, xloc.bar_index, color = color(na), textcolor = equalColor, style = labelStyle, size = INPUT_EQ_HL_SIZE)

// @function            store current structure and trailing swing points, and also display swing points and equal highs/lows
// @param size          (int) structure size
// @param equalHighLow  (bool) true for displaying current highs/lows
// @param internal      (bool) true for getting internal structures
// @returns             label ID
getCurrentStructure(int size,bool equalHighLow = false, bool internal = false) =>        
    currentLeg              = leg(size)
    newPivot                = startOfNewLeg(currentLeg)
    pivotLow                = startOfBullishLeg(currentLeg)
    pivotHigh               = startOfBearishLeg(currentLeg)

    if newPivot
        if pivotLow
            pivot p_ivot    = equalHighLow ? equalLow : internal ? internalLow : swingLow    

            if equalHighLow and math.abs(p_ivot.currentLevel - low[size]) < INPUT_EQ_HL_THRESHOLD * atrMeasure                
                drawEqualHighLow(p_ivot, low[size], size, false)

            p_ivot.lastLevel    := p_ivot.currentLevel
            p_ivot.currentLevel := low[size]
            p_ivot.crossed      := false
            p_ivot.barTime      := time[size]
            p_ivot.barIndex     := bar_index[size]

            if not equalHighLow and not internal
                trailing.bottom         := p_ivot.currentLevel
                trailing.barTime        := p_ivot.barTime
                trailing.barIndex       := p_ivot.barIndex
                trailing.lastBottomTime := p_ivot.barTime

            if INPUT_SHOW_SWINGS and not internal and not equalHighLow
                drawLabel(time[size], p_ivot.currentLevel, p_ivot.currentLevel < p_ivot.lastLevel ? 'LL' : 'HL', swingBullishColor, label.style_label_up)            
        else
            pivot p_ivot = equalHighLow ? equalHigh : internal ? internalHigh : swingHigh

            if equalHighLow and math.abs(p_ivot.currentLevel - high[size]) < INPUT_EQ_HL_THRESHOLD * atrMeasure
                drawEqualHighLow(p_ivot,high[size],size,true)                

            p_ivot.lastLevel    := p_ivot.currentLevel
            p_ivot.currentLevel := high[size]
            p_ivot.crossed      := false
            p_ivot.barTime      := time[size]
            p_ivot.barIndex     := bar_index[size]

            if not equalHighLow and not internal
                trailing.top            := p_ivot.currentLevel
                trailing.barTime        := p_ivot.barTime
                trailing.barIndex       := p_ivot.barIndex
                trailing.lastTopTime    := p_ivot.barTime

            if INPUT_SHOW_SWINGS and not internal and not equalHighLow
                drawLabel(time[size], p_ivot.currentLevel, p_ivot.currentLevel > p_ivot.lastLevel ? 'HH' : 'LH', swingBearishColor, label.style_label_down)
                
// @function                draw line and label representing a structure
// @param p_ivot            base pivot point
// @param tag               test to display
// @param structureColor    base color
// @param lineStyle         line style
// @param labelStyle        label style
// @param labelSize         text size
// @returns                 label ID
drawStructure(pivot p_ivot, string tag, color structureColor, string lineStyle, string labelStyle, string labelSize) =>    
    var line l_ine      = line.new(na,na,na,na,xloc = xloc.bar_time)
    var label l_abel    = label.new(na,na)

    if INPUT_MODE == PRESENT
        l_ine.delete()
        l_abel.delete()

    l_ine   := line.new(chart.point.new(p_ivot.barTime,na,p_ivot.currentLevel), chart.point.new(time,na,p_ivot.currentLevel), xloc.bar_time, color=structureColor, style=lineStyle)
    l_abel  := label.new(chart.point.new(na,math.round(0.5*(p_ivot.barIndex+bar_index)),p_ivot.currentLevel), tag, xloc.bar_index, color=color(na), textcolor=structureColor, style=labelStyle, size = labelSize)

// @function            delete order blocks
// @param internal      true for internal order blocks
// @returns             orderBlock ID
deleteOrderBlocks(bool internal = false) =>
    array<orderBlock> orderBlocks = internal ? internalOrderBlocks : swingOrderBlocks

    for [index,eachOrderBlock] in orderBlocks
        bool crossedOderBlock = false
        
        if bearishOrderBlockMitigationSource > eachOrderBlock.barHigh and eachOrderBlock.bias == BEARISH
            crossedOderBlock := true
            if internal
                currentAlerts.internalBearishOrderBlock := true
            else
                currentAlerts.swingBearishOrderBlock    := true
        else if bullishOrderBlockMitigationSource < eachOrderBlock.barLow and eachOrderBlock.bias == BULLISH
            crossedOderBlock := true
            if internal
                currentAlerts.internalBullishOrderBlock := true
            else
                currentAlerts.swingBullishOrderBlock    := true
        if crossedOderBlock                    
            orderBlocks.remove(index)            

// @function            fetch and store order blocks
// @param p_ivot        base pivot point
// @param internal      true for internal order blocks
// @param bias          BULLISH or BEARISH
// @returns             void
storeOrdeBlock(pivot p_ivot,bool internal = false,int bias) =>
    if (not internal and INPUT_SHOW_SWING_OB) or (internal and INPUT_SHOW_INTERNAL_OB)

        array<float> a_rray = na
        int parsedIndex = na

        if bias == BEARISH
            a_rray      := parsedHighs.slice(p_ivot.barIndex,bar_index)
            parsedIndex := p_ivot.barIndex + a_rray.indexof(a_rray.max())  
        else
            a_rray      := parsedLows.slice(p_ivot.barIndex,bar_index)
            parsedIndex := p_ivot.barIndex + a_rray.indexof(a_rray.min())                        

        orderBlock o_rderBlock          = orderBlock.new(parsedHighs.get(parsedIndex), parsedLows.get(parsedIndex), times.get(parsedIndex),bias)
        array<orderBlock> orderBlocks   = internal ? internalOrderBlocks : swingOrderBlocks
        
        if orderBlocks.size() >= 100
            orderBlocks.pop()
        orderBlocks.unshift(o_rderBlock)

// @function            draw order blocks as boxes
// @param internal      true for internal order blocks
// @returns             void
drawOrderBlocks(bool internal = false) =>        
    array<orderBlock> orderBlocks = internal ? internalOrderBlocks : swingOrderBlocks
    orderBlocksSize = orderBlocks.size()

    if orderBlocksSize > 0        
        maxOrderBlocks                      = internal ? INPUT_INTERNAL_OB_SIZE : INPUT_SWING_OB_SIZE
        array<orderBlock> parsedOrdeBlocks  = orderBlocks.slice(0, math.min(maxOrderBlocks,orderBlocksSize))
        array<box> b_oxes                   = internal ? internalOrderBlocksBoxes : swingOrderBlocksBoxes        

        for [index,eachOrderBlock] in parsedOrdeBlocks
            orderBlockColor = INPUT_STYLE == MONOCHROME ? (eachOrderBlock.bias == BEARISH ? color.new(MONO_BEARISH,80) : color.new(MONO_BULLISH,80)) : internal ? (eachOrderBlock.bias == BEARISH ? INPUT_INTERNAL_BEAR_OB_COLOR : INPUT_INTERNAL_BULL_OB_COLOR) : (eachOrderBlock.bias == BEARISH ? INPUT_SWING_BEAR_OB_COLOR : INPUT_SWING_BULL_OB_COLOR)

            box b_ox        = b_oxes.get(index)
            b_ox.set_top_left_point(    chart.point.new(eachOrderBlock.barTime,na,eachOrderBlock.barHigh))
            b_ox.set_bottom_right_point(chart.point.new(last_bar_time,na,eachOrderBlock.barLow))        
            b_ox.set_border_color(      internal ? na : orderBlockColor)
            b_ox.set_bgcolor(           orderBlockColor)

// @function            detect and draw structures, also detect and store order blocks
// @param internal      true for internal structures or order blocks
// @returns             void
displayStructure(bool internal = false) =>
    var bullishBar = true
    var bearishBar = true

    if INPUT_INT_FILTER_CONFLUENCE
        bullishBar := high - math.max(close, open) > math.min(close, open - low)
        bearishBar := high - math.max(close, open) < math.min(close, open - low)
    
    pivot p_ivot    = internal ? internalHigh : swingHigh
    trend t_rend    = internal ? internalTrend : swingTrend

    lineStyle       = internal ? line.style_dashed : line.style_solid
    labelSize       = internal ? INPUT_INTERNAL_LABEL_SIZE : INPUT_SWING_LABEL_SIZE

    extraCondition  = internal ? internalHigh.currentLevel != swingHigh.currentLevel and bullishBar : true
    bullishColor    = INPUT_STYLE == MONOCHROME ? MONO_BULLISH : internal ? INPUT_INTERNAL_BULL_COLOR : INPUT_SWING_BULL_COLOR

    if ta.crossover(close,p_ivot.currentLevel) and not p_ivot.crossed and extraCondition
        string tag = t_rend.bias == BEARISH ? CHOCH : BOS

        if internal
            currentAlerts.internalBullishCHoCH  := tag == CHOCH
            currentAlerts.internalBullishBOS    := tag == BOS
        else
            currentAlerts.swingBullishCHoCH     := tag == CHOCH
            currentAlerts.swingBullishBOS       := tag == BOS

        p_ivot.crossed  := true
        t_rend.bias     := BULLISH

        displayCondition = internal ? INPUT_SHOW_INTERNALS and (INPUT_INTERNAL_BULL_STRUCTURE == ALL or (INPUT_INTERNAL_BULL_STRUCTURE == BOS and tag != CHOCH) or (INPUT_INTERNAL_BULL_STRUCTURE == CHOCH and tag == CHOCH)) : INPUT_SHOW_SWING_STRUCTURE and (INPUT_SWING_BULL_STRUCTURE == ALL or (INPUT_SWING_BULL_STRUCTURE == BOS and tag != CHOCH) or (INPUT_SWING_BULL_STRUCTURE == CHOCH and tag == CHOCH))

        if displayCondition                        
            drawStructure(p_ivot,tag,bullishColor,lineStyle,label.style_label_down,labelSize)

        if (internal and INPUT_SHOW_INTERNAL_OB) or (not internal and INPUT_SHOW_SWING_OB)
            storeOrdeBlock(p_ivot,internal,BULLISH)

    p_ivot          := internal ? internalLow : swingLow    
    extraCondition  := internal ? internalLow.currentLevel != swingLow.currentLevel and bearishBar : true
    bearishColor    = INPUT_STYLE == MONOCHROME ? MONO_BEARISH : internal ? INPUT_INTERNAL_BEAR_COLOR : INPUT_SWING_BEAR_COLOR

    if ta.crossunder(close,p_ivot.currentLevel) and not p_ivot.crossed and extraCondition
        string tag = t_rend.bias == BULLISH ? CHOCH : BOS

        if internal
            currentAlerts.internalBearishCHoCH  := tag == CHOCH
            currentAlerts.internalBearishBOS    := tag == BOS
        else
            currentAlerts.swingBearishCHoCH     := tag == CHOCH
            currentAlerts.swingBearishBOS       := tag == BOS

        p_ivot.crossed := true
        t_rend.bias := BEARISH

        displayCondition = internal ? INPUT_SHOW_INTERNALS and (INPUT_INTERNAL_BEAR_STRUCTURE == ALL or (INPUT_INTERNAL_BEAR_STRUCTURE == BOS and tag != CHOCH) or (INPUT_INTERNAL_BEAR_STRUCTURE == CHOCH and tag == CHOCH)) : INPUT_SHOW_SWING_STRUCTURE and (INPUT_SWING_BEAR_STRUCTURE == ALL or (INPUT_SWING_BEAR_STRUCTURE == BOS and tag != CHOCH) or (INPUT_SWING_BEAR_STRUCTURE == CHOCH and tag == CHOCH))
        
        if displayCondition                        
            drawStructure(p_ivot,tag,bearishColor,lineStyle,label.style_label_up,labelSize)

        if (internal and INPUT_SHOW_INTERNAL_OB) or (not internal and INPUT_SHOW_SWING_OB)
            storeOrdeBlock(p_ivot,internal,BEARISH)

// @function            draw one fair value gap box (each fair value gap has two boxes)
// @param leftTime      left time coordinate
// @param rightTime     right time coordinate
// @param topPrice      top price level
// @param bottomPrice   bottom price level
// @param boxColor      box color
// @returns             box ID
fairValueGapBox(leftTime,rightTime,topPrice,bottomPrice,boxColor) => box.new(chart.point.new(leftTime,na,topPrice),chart.point.new(rightTime + INPUT_FVG_EXTEND * (time-time[1]),na,bottomPrice), xloc=xloc.bar_time, border_color = boxColor, bgcolor = boxColor)

// @function            delete fair value gaps
// @returns             fairValueGap ID
deleteFairValueGaps() =>
    for [index,eachFairValueGap] in fairValueGaps
        if (low < eachFairValueGap.bottom and eachFairValueGap.bias == BULLISH) or (high > eachFairValueGap.top and eachFairValueGap.bias == BEARISH)
            eachFairValueGap.topBox.delete()
            eachFairValueGap.bottomBox.delete()
            fairValueGaps.remove(index)
    
// @function            draw fair value gaps
// @returns             fairValueGap ID
drawFairValueGaps() => 
    [lastClose, lastOpen, lastTime, currentHigh, currentLow, currentTime, last2High, last2Low] = request.security(syminfo.tickerid, INPUT_FVG_TIMEFRAME, [close[1], open[1], time[1], high[0], low[0], time[0], high[2], low[2]],lookahead = barmerge.lookahead_on)

    barDeltaPercent     = (lastClose - lastOpen) / (lastOpen * 100)
    newTimeframe        = timeframe.change(INPUT_FVG_TIMEFRAME)
    threshold           = INPUT_FVG_AUTO_THRESHOLD ? ta.cum(math.abs(newTimeframe ? barDeltaPercent : 0)) / bar_index * 2 : 0

    bullishFairValueGap = currentLow > last2High and lastClose > last2High and barDeltaPercent > threshold and newTimeframe
    bearishFairValueGap = currentHigh < last2Low and lastClose < last2Low and -barDeltaPercent > threshold and newTimeframe

    if bullishFairValueGap
        currentAlerts.bullishFairValueGap := true
        fairValueGaps.unshift(fairValueGap.new(currentLow,last2High,BULLISH,fairValueGapBox(lastTime,currentTime,currentLow,math.avg(currentLow,last2High),fairValueGapBullishColor),fairValueGapBox(lastTime,currentTime,math.avg(currentLow,last2High),last2High,fairValueGapBullishColor)))
    if bearishFairValueGap
        currentAlerts.bearishFairValueGap := true
        fairValueGaps.unshift(fairValueGap.new(currentHigh,last2Low,BEARISH,fairValueGapBox(lastTime,currentTime,currentHigh,math.avg(currentHigh,last2Low),fairValueGapBearishColor),fairValueGapBox(lastTime,currentTime,math.avg(currentHigh,last2Low),last2Low,fairValueGapBearishColor)))

// @function            get line style from string
// @param style         line style
// @returns             string
getStyle(string style) =>
    switch style
        SOLID => line.style_solid
        DASHED => line.style_dashed
        DOTTED => line.style_dotted

// @function            draw MultiTimeFrame levels
// @param timeframe     base timeframe
// @param sameTimeframe true if chart timeframe is same as base timeframe
// @param style         line style
// @param levelColor    line and text color
// @returns             void
drawLevels(string timeframe, bool sameTimeframe, string style, color levelColor) =>
    [topLevel, bottomLevel, leftTime, rightTime] = request.security(syminfo.tickerid, timeframe, [high[1], low[1], time[1], time],lookahead = barmerge.lookahead_on)

    float parsedTop         = sameTimeframe ? high : topLevel
    float parsedBottom      = sameTimeframe ? low : bottomLevel    

    int parsedLeftTime      = sameTimeframe ? time : leftTime
    int parsedRightTime     = sameTimeframe ? time : rightTime

    int parsedTopTime       = time
    int parsedBottomTime    = time

    if not sameTimeframe
        int leftIndex               = times.binary_search_rightmost(parsedLeftTime)
        int rightIndex              = times.binary_search_rightmost(parsedRightTime)

        array<int> timeArray        = times.slice(leftIndex,rightIndex)
        array<float> topArray       = highs.slice(leftIndex,rightIndex)
        array<float> bottomArray    = lows.slice(leftIndex,rightIndex)

        parsedTopTime               := timeArray.size() > 0 ? timeArray.get(topArray.indexof(topArray.max())) : initialTime
        parsedBottomTime            := timeArray.size() > 0 ? timeArray.get(bottomArray.indexof(bottomArray.min())) : initialTime

    var line topLine        = line.new(na, na, na, na, xloc = xloc.bar_time, color = levelColor, style = getStyle(style))
    var line bottomLine     = line.new(na, na, na, na, xloc = xloc.bar_time, color = levelColor, style = getStyle(style))
    var label topLabel      = label.new(na, na, xloc = xloc.bar_time, text = str.format('P{0}H',timeframe), color=color(na), textcolor = levelColor, size = size.small, style = label.style_label_left)
    var label bottomLabel   = label.new(na, na, xloc = xloc.bar_time, text = str.format('P{0}L',timeframe), color=color(na), textcolor = levelColor, size = size.small, style = label.style_label_left)

    topLine.set_first_point(    chart.point.new(parsedTopTime,na,parsedTop))
    topLine.set_second_point(   chart.point.new(last_bar_time + 20 * (time-time[1]),na,parsedTop))   
    topLabel.set_point(         chart.point.new(last_bar_time + 20 * (time-time[1]),na,parsedTop))

    bottomLine.set_first_point( chart.point.new(parsedBottomTime,na,parsedBottom))    
    bottomLine.set_second_point(chart.point.new(last_bar_time + 20 * (time-time[1]),na,parsedBottom))
    bottomLabel.set_point(      chart.point.new(last_bar_time + 20 * (time-time[1]),na,parsedBottom))

// @function            true if chart timeframe is higher than provided timeframe
// @param timeframe     timeframe to check
// @returns             bool
higherTimeframe(string timeframe) => timeframe.in_seconds() > timeframe.in_seconds(timeframe)

// @function            update trailing swing points
// @returns             int
updateTrailingExtremes() =>
    trailing.top            := math.max(high,trailing.top)
    trailing.lastTopTime    := trailing.top == high ? time : trailing.lastTopTime
    trailing.bottom         := math.min(low,trailing.bottom)
    trailing.lastBottomTime := trailing.bottom == low ? time : trailing.lastBottomTime

// @function            draw trailing swing points
// @returns             void
drawHighLowSwings() =>
    var line topLine        = line.new(na, na, na, na, color = swingBearishColor, xloc = xloc.bar_time)
    var line bottomLine     = line.new(na, na, na, na, color = swingBullishColor, xloc = xloc.bar_time)
    var label topLabel      = label.new(na, na, color=color(na), textcolor = swingBearishColor, xloc = xloc.bar_time, style = label.style_label_down, size = size.tiny)
    var label bottomLabel   = label.new(na, na, color=color(na), textcolor = swingBullishColor, xloc = xloc.bar_time, style = label.style_label_up, size = size.tiny)

    rightTimeBar            = last_bar_time + 20 * (time - time[1])

    topLine.set_first_point(    chart.point.new(trailing.lastTopTime, na, trailing.top))
    topLine.set_second_point(   chart.point.new(rightTimeBar, na, trailing.top))
    topLabel.set_point(         chart.point.new(rightTimeBar, na, trailing.top))
    topLabel.set_text(          swingTrend.bias == BEARISH ? STRONG_HIGH : WEAK_HIGH)

    bottomLine.set_first_point( chart.point.new(trailing.lastBottomTime, na, trailing.bottom))
    bottomLine.set_second_point(chart.point.new(rightTimeBar, na, trailing.bottom))
    bottomLabel.set_point(      chart.point.new(rightTimeBar, na, trailing.bottom))
    bottomLabel.set_text(       swingTrend.bias == BULLISH ? STRONG_LOW: WEAK_LOW)

// @function            draw a zone with a label and a box
// @param labelLevel    price level for label
// @param labelIndex    bar index for label
// @param top           top price level for box
// @param bottom        bottom price level for box
// @param tag           text to display
// @param zoneColor     base color
// @param style         label style
// @returns             void
drawZone(float labelLevel, int labelIndex, float top, float bottom, string tag, color zoneColor, string style) =>
    var label l_abel    = label.new(na,na,text = tag, color=color(na),textcolor = zoneColor, style = style, size = size.small)
    var box b_ox        = box.new(na,na,na,na,bgcolor = color.new(zoneColor,80),border_color = color(na), xloc = xloc.bar_time)

    b_ox.set_top_left_point(    chart.point.new(trailing.barTime,na,top))
    b_ox.set_bottom_right_point(chart.point.new(last_bar_time,na,bottom))

    l_abel.set_point(           chart.point.new(na,labelIndex,labelLevel))

// @function            draw premium/discount zones
// @returns             void
drawPremiumDiscountZones() =>
    drawZone(trailing.top, math.round(0.5*(trailing.barIndex + last_bar_index)), trailing.top, 0.95*trailing.top + 0.05*trailing.bottom, PREMIUM, premiumZoneColor, label.style_label_down)

    equilibriumLevel = math.avg(trailing.top, trailing.bottom)
    drawZone(equilibriumLevel, last_bar_index, 0.525*trailing.top + 0.475*trailing.bottom, 0.525*trailing.bottom + 0.475*trailing.top, EQUILIBRIUM, INPUT_EQL_ZONE_COLOR, label.style_label_left)

    drawZone(trailing.bottom, math.round(0.5*(trailing.barIndex + last_bar_index)), 0.95*trailing.bottom + 0.05*trailing.top, trailing.bottom, DISCOUNT, discountZoneColor, label.style_label_up)


//-----------------------------------------------------------------------------}
//Function
//-----------------------------------------------------------------------------{
pred_ranges(length_pred, mult_pred)=>
    var avg = INPUT_SOURCE
    var hold_atr = 0.

    atr = nz(ta.atr(length_pred)) * mult_pred
        
    avg := INPUT_SOURCE - avg > atr ? avg + atr : 
      avg - INPUT_SOURCE > atr ? avg - atr : 
      avg
        
    hold_atr := avg != avg[1] ? atr / 2 : hold_atr
        
    [avg + hold_atr * 2, avg + hold_atr, avg, avg - hold_atr, avg - hold_atr * 2]

//-----------------------------------------------------------------------------}
//Detect and highlight ranges [OPTIMIZED - DONCHIAN LOGIC]
//-----------------------------------------------------------------------------{
//Ranges drawings
var box bx = na
var line lvl = na

//Range State
var bool is_ranging = false

//Inputs for sensitivity
range_max_height = ta.atr(INPUT_ATR_LENGTH) * INPUT_RANGE_WIDTH // Maximum allowed height of the box

//Donchian Channel Logic (Smart Box Detection)
highest_high = ta.highest(high, INPUT_RANGE_LENGTH)
lowest_low   = ta.lowest(low, INPUT_RANGE_LENGTH)
box_height   = highest_high - lowest_low

//Check if the height of the last 'length' bars is tight enough to be a range
is_tight = box_height < range_max_height

if is_tight
    //If we are already in a range, just extend it
    if is_ranging
        //Update the right side of the box to current bar
        bx.set_right(bar_index)
        lvl.set_x2(bar_index)
        
        //Optional: Slightly expand vertical limits if price pushes edge but stays tight
        current_top = bx.get_top()
        current_bot = bx.get_bottom()
        if high > current_top and (high - current_bot) < range_max_height
            bx.set_top(high)
        if low < current_bot and (current_top - low) < range_max_height
            bx.set_bottom(low)
            
    else
        //NEW RANGE DETECTED: Draw the box
        is_ranging := true
        
        //Determine color based on where we entered (optional logic, defaults to gray until breakout)
        box_color = color.new(INPUT_UNBROKEN_CSS, 80)
        
        bx := box.new(bar_index[INPUT_RANGE_LENGTH], highest_high, bar_index, lowest_low, na, bgcolor = box_color)
        lvl := line.new(bar_index[INPUT_RANGE_LENGTH], math.avg(highest_high, lowest_low), bar_index, math.avg(highest_high, lowest_low), color = INPUT_UNBROKEN_CSS, style = line.style_dotted)

else
    //Range Broken - Stop updating
    if is_ranging
        //Check Breakout Direction for final color
        if close > bx.get_top()
            bx.set_bgcolor(color.new(INPUT_UP_CSS, 75))
            lvl.set_color(INPUT_UP_CSS)
        else if close < bx.get_bottom()
            bx.set_bgcolor(color.new(INPUT_DN_CSS, 75))
            lvl.set_color(INPUT_DN_CSS)
        
        is_ranging := false



//-----------------------------------------------------------------------------}
//Plots
//-----------------------------------------------------------------------------{
// 1. RESTORE THE CALCULATIONS (Crucial: This was likely deleted in the edit)
[prR2, prR1, avg, prS1, prS2] = request.security(syminfo.tickerid, INPUT_TIMEFRAME, pred_ranges(INPUT_PRED_LENGTH, INPUT_PRED_FACTOR))

// 2. DEFINE BACKGROUND COLOR (Linked to the new "is_ranging" logic)
// If we are currently in a tight range (is_ranging = true), show the white background.
color detect_css = is_ranging ? color.new(color.white, 90) : na
bgcolor(detect_css)

// 3. PLOTS (Predictive Ranges)
plot_pru2  = plot(prR2, 'PR Upper 2', avg != avg[1] ? na : color.new(RED, 25), linewidth = 4)
plot_pru1  = plot(prR1, 'PR Upper 1', avg != avg[1] ? na : color.new(RED, 25), linewidth = 2)
plot_pravg = plot(avg , 'PR Average', avg != avg[1] ? na : color.new(BLUE, 25), linewidth = 3)
plot_prl1  = plot(prS1, 'PR Lower 1', avg != avg[1] ? na : color.new(GREEN, 25), linewidth = 2)
plot_prl2  = plot(prS2, 'PR Lower 2', avg != avg[1] ? na : color.new(GREEN, 25), linewidth = 4)

// 4. FILLS
fill(plot_pru2, plot_pru1, avg != avg[1] ? na : color.new(RED, 95))
fill(plot_prl1, plot_prl2, avg != avg[1] ? na : color.new(GREEN, 95))


//-----------------------------------------------------------------------------}

//---------------------------------------------------------------------------------------------------------------------}
//MUTABLE VARIABLES & EXECUTION
//---------------------------------------------------------------------------------------------------------------------{
parsedOpen  = INPUT_SHOW_TREND ? open : na
candleColor = internalTrend.bias == BULLISH ? swingBullishColor : swingBearishColor
plotcandle(parsedOpen,high,low,close,color = candleColor, wickcolor = candleColor, bordercolor = candleColor)

if INPUT_SHOW_HL_SWINGS or INPUT_SHOW_PD_ZONES
    updateTrailingExtremes()

    if INPUT_SHOW_HL_SWINGS
        drawHighLowSwings()

    if INPUT_SHOW_PD_ZONES
        drawPremiumDiscountZones()

if INPUT_SHOW_FVG
    deleteFairValueGaps()

getCurrentStructure(INPUT_SWINGS_LENGTH,false)
getCurrentStructure(5,false,true)

if INPUT_SHOW_EQ_HL
    getCurrentStructure(INPUT_EQ_HL_LENGTH,true)

if INPUT_SHOW_INTERNALS or INPUT_SHOW_INTERNAL_OB or INPUT_SHOW_TREND
    displayStructure(true)

if INPUT_SHOW_SWING_STRUCTURE or INPUT_SHOW_SWING_OB or INPUT_SHOW_HL_SWINGS
    displayStructure()

if INPUT_SHOW_INTERNAL_OB
    deleteOrderBlocks(true)

if INPUT_SHOW_SWING_OB
    deleteOrderBlocks()

if INPUT_SHOW_FVG
    drawFairValueGaps()

if barstate.islastconfirmedhistory or barstate.islast
    if INPUT_SHOW_INTERNAL_OB        
        drawOrderBlocks(true)
        
    if INPUT_SHOW_SWING_OB        
        drawOrderBlocks()

lastBarIndex    := currentBarIndex
currentBarIndex := bar_index
newBar          = currentBarIndex != lastBarIndex

if barstate.islastconfirmedhistory or (barstate.isrealtime and newBar)
    if INPUT_SHOW_DAILY_LEVELS and not higherTimeframe('D')
        drawLevels('D',timeframe.isdaily,INPUT_DAILY_LEVELS_STYLE,INPUT_DAILY_LEVELS_COLOR)

    if INPUT_SHOW_WEEKLY_LEVELS and not higherTimeframe('W')
        drawLevels('W',timeframe.isweekly,INPUT_WEEKLY_LEVELS_STYLE,INPUT_WEEKLY_LEVELS_COLOR)

    if INPUT_SHOW_MONTHLY_LEVELS and not higherTimeframe('M')
        drawLevels('M',timeframe.ismonthly,INPUT_MONTHLY_LEVELS_STYLE,INPUT_MONTHLY_LEVELS_COLOR)


//---------------------------------------------------------------------------------------------------------------------}
//ALERTS
//---------------------------------------------------------------------------------------------------------------------{
alertcondition(currentAlerts.internalBullishBOS,        'Internal Bullish BOS',         'Internal Bullish BOS formed')
alertcondition(currentAlerts.internalBullishCHoCH,      'Internal Bullish CHoCH',       'Internal Bullish CHoCH formed')
alertcondition(currentAlerts.internalBearishBOS,        'Internal Bearish BOS',         'Internal Bearish BOS formed')
alertcondition(currentAlerts.internalBearishCHoCH,      'Internal Bearish CHoCH',       'Internal Bearish CHoCH formed')

alertcondition(currentAlerts.swingBullishBOS,           'Bullish BOS',                  'Internal Bullish BOS formed')
alertcondition(currentAlerts.swingBullishCHoCH,         'Bullish CHoCH',                'Internal Bullish CHoCH formed')
alertcondition(currentAlerts.swingBearishBOS,           'Bearish BOS',                  'Bearish BOS formed')
alertcondition(currentAlerts.swingBearishCHoCH,         'Bearish CHoCH',                'Bearish CHoCH formed')

alertcondition(currentAlerts.internalBullishOrderBlock, 'Bullish Internal OB Breakout', 'Price broke bullish internal OB')
alertcondition(currentAlerts.internalBearishOrderBlock, 'Bearish Internal OB Breakout', 'Price broke bearish internal OB')
alertcondition(currentAlerts.swingBullishOrderBlock,    'Bullish Swing OB Breakout',    'Price broke bullish swing OB')
alertcondition(currentAlerts.swingBearishOrderBlock,    'Bearish Swing OB Breakout',    'Price broke bearish swing OB')

alertcondition(currentAlerts.equalHighs,                'Equal Highs',                  'Equal highs detected')
alertcondition(currentAlerts.equalLows,                 'Equal Lows',                   'Equal lows detected')

alertcondition(currentAlerts.bullishFairValueGap,       'Bullish FVG',                  'Bullish FVG formed')
alertcondition(currentAlerts.bearishFairValueGap,       'Bearish FVG',                  'Bearish FVG formed')

//---------------------------------------------------------------------------------------------------------------------}